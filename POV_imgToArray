/*

POV - Flavia Laudado _ Mayo 2019

 Dibujar en una cuadrícula
 y exportar los datos en un array 
 También guarda cada dibujo
 
 Para desarrollar dibujos respresentados
 con una varita POV
 
 //CONTROLES//
 click izq negro
 click der blanco
 enter guardar img y array de datos
 barra limpia la pantalla
 
 */

int col = 20;
int filas = 20;
int anchoPixel, altoPixel;
float nH, nV;
color pixel;

PImage imgOut;

PrintWriter output;
int contador = 0;

void setup() {
  fullScreen();
  //size(800, 1000);
  background(255);
  //rect0();
  output = createWriter("datos.txt");

  imgOut = createImage(width, height, ALPHA); 
  //inicializacion cuadricula
  anchoPixel = width/col;
  altoPixel = height/filas;
}

void draw() {
  dibujar();
  cuadricula();
}

void dibujar() { //dibuja en cada cuadrante correspondiente
  if (mousePressed == true) {
    if (mouseButton == LEFT) {
      fill(0);
    } else if (mouseButton == RIGHT) {
      fill(255);
    }
    nH = mouseX/anchoPixel;
    nV = mouseY/altoPixel;
    rect(nH * anchoPixel, nV * altoPixel, anchoPixel, altoPixel);
  }
}

void cuadricula() {
  for (int i = 0; i <= col; i++) {
    int xcol = anchoPixel * i;
    line(xcol, 0, xcol, height);
  }

  for (int i = 0; i <= filas; i++) {
    int yFilas = altoPixel * i;
    line(0, yFilas, width, yFilas);
  }
}

void keyPressed() {
  if (key==ENTER) {
    imgOut = get();
    saveFrame("img####.jpg");
    int mitadAnchoPixel = anchoPixel/2;
    int mitadAltoPixel = altoPixel/2;

    print("Dibujo nro " + contador + ": ");
    output.print("Dibujo nro " + contador + ": ");

    for (int j =mitadAltoPixel; j<imgOut.height; j += altoPixel) {
      for (int i = mitadAnchoPixel; i<imgOut.width; i += anchoPixel) {
        color c = imgOut.get(i, j);
        float col = red(c);
        int colInt = int(col);

        print(colInt + ", ");

        output.print(colInt + ", ");
      }
    }
    output.println(" ");
    output.println(" ");
    output.flush(); 
    //output.close();

    println(" ");
    contador++;
  }

  if (key==' ') {
    fill(255);
    rect(0, 0, width, height);
  }
}
